#!/usr/bin/env coffee

sys = require 'sys'
fs = require 'fs'
path = require 'path'

puts = (output) -> sys.puts output

pad = (output) ->
  puts ""
  output()
  puts ""

load_tasks = (file, callback) ->
  try
    data = if path.existsSync(file)
      JSON.parse fs.readFileSync(file)
    else
      tasks: [], completions: {}
    callback data.tasks, data.completions
  catch error
    puts "There was a problem reading your .doit file: '#{error}'"

add = (task, tasks, callback) ->
  if task in tasks
    puts "Task already in list"
  else
    tasks.push(task)
    callback tasks

did = (task, tasks, completions, date, callback) ->
  completions[date] ?= []

  if task not in tasks
    puts "Not a valid task"
  else if task in completions[date]
    puts "Task already completed"
  else
    completions[date].push(task)
    callback completions

save = (tasks, completions, file, callback) ->
  data = tasks: tasks, completions: completions
  fs.writeFile file, JSON.stringify(data, undefined, 2), (err) ->
    if err
      puts err
    else
      callback()

print = (tasks, completions, date) ->
  pad ->
    if tasks.length == 0
      puts "(use `doit add <task>` to add a task)"
    else
      unless date == (new Date).toDateString()
        puts "  #{date}\n"
      for task in tasks.sort()
        puts "  #{ if task in completions[date] then "X" else "_" } #{task}"

chart = (tasks, completions, date) ->
  pad ->
    puts "           | #{ tasks.map((t) -> t[0..3]).join " | " }"
    puts "-----------+------+------+------+------+------"
    for i in [6..0]
      curdate = new Date(date)
      curdate.setDate curdate.getDate() - i
      curdate = curdate.toDateString()

      completed = tasks.map (task) ->
        if task in completions[curdate]
          "XXXX"
        else
          "    "
      puts "#{curdate[0..-6]} | #{ completed.join " | " }"

file = "#{process.env.HOME}/.doit"

date = new Date
date.setDate date.getDate() - 1 if "yesterday" in process.argv
date = date.toDateString()

[command, task] = process.argv[2..3]

load_tasks file, (tasks, completions) ->
  switch command
    when "add"
      add task, tasks, (tasks) ->
        save tasks, completions, file, ->
          print tasks, completions, date
    when "did"
      did task, tasks, completions, date, (completions) ->
        save tasks, completions, file, ->
          print tasks, completions, date
    when "chart"
      chart tasks, completions, date
    else
      print tasks, completions, date
