#!/usr/bin/env coffee

sys = require 'sys'
fs = require 'fs'
path = require 'path'

puts = (output) -> sys.puts output

pad = (output) ->
  puts ""
  output()
  puts ""

date_string = (opts = {}) ->
  date = if opts.init
    new Date(opts.init)
  else
    new Date
  date.setDate date.getDate() - (opts.offset || 0)
  date.toDateString()

load_tasks = (file, callback) ->
  report_problem = (error) ->
    puts "There was a problem reading your .doit file: '#{error}'"

  path.exists file, (exists) ->
    if exists
      fs.readFile file, (error, data) ->
        if error
          report_problem error
        else
          try
            parsed = JSON.parse data
            callback parsed.tasks, parsed.completions
          catch error
            report_problem error
    else
      callback [], {}

add = (task, tasks, callback) ->
  if !task?
    puts "Please enter a task"
  else if task in tasks
    puts "Task already in list"
  else
    tasks.push(task)
    callback tasks

did = (task, tasks, completions, date, callback) ->
  completions[date] ?= []

  if task not in tasks
    puts "Not a valid task"
  else if task in completions[date]
    puts "Task already completed"
  else
    completions[date].push(task)
    callback completions

save = (tasks, completions, file, callback) ->
  data = tasks: tasks, completions: completions
  fs.writeFile file, JSON.stringify(data, undefined, 2), (err) ->
    if err
      puts err
    else
      callback()

print = (tasks, completions, date) ->
  pad ->
    if tasks.length == 0
      puts "(use `doit add <task>` to add a task)"
    else
      unless date == date_string()
        puts "  #{date}\n"
      for task in tasks.sort()
        puts "  #{ if task in completions[date] then "X" else "_" } #{task}"

chart = (tasks, completions, date) ->
  task_string = (str) -> tasks.sort().map(str).join ''

  pad ->
    puts "             " + task_string (t) -> "| #{t}    "[0..6]
    puts "  -----------" + task_string (t) -> "+------"

    for i in [6..0]
      curdate = date_string init: date, offset: i
      completed = tasks.map (task) ->
        if task in completions[curdate] then "XXXX" else "    "
      puts "  #{curdate[0..-6]} | #{ completed.join " | " }"

file = "#{process.env.HOME}/.doit"

date = date_string offset: (1 if "yesterday" in process.argv)

[command, task] = process.argv[2..3]

load_tasks file, (tasks, completions) ->
  switch command
    when "add"
      add task, tasks, (tasks) ->
        save tasks, completions, file, ->
          print tasks, completions, date
    when "did"
      did task, tasks, completions, date, (completions) ->
        save tasks, completions, file, ->
          print tasks, completions, date
    when "chart"
      chart tasks, completions, date
    else
      print tasks, completions, date
